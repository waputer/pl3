/**
 * Top level structure.
 *   @unit: The list of code units.
 */
tydef[export] st:pl3:top {
	pt:st:pl3:unit unit;
};

/**
 * Code unit structure.
 *   @stmt: The statement list.
 *   @next: The next code unit.
 */
tydef[export] st:pl3:unit {
	pt:st:pl3:stmt stmt;
	pt:st:pl3:unit next;
};


/**
 * Create a blank top-level.
 *   &returns: The top-level.
 */
def[export] pt:st:pl3:top() pl3:top_new = {
	return mem:make(st:pl3:top{ null });
};

/**
 * Delete a top-level.
 *   @top: The top-level.
 */
def[export] void(pt:st:pl3:top top) pl3:top_delete = {
	var pt:st:pl3:unit unit;

	while(top->unit != null) {
		top->unit = (unit = top->unit)->next;

		pl3:stmt_clear(unit->stmt);
		mem:free(unit);
	}

	mem:free(top);
};

/**
 * Add a code unit to the top-level.
 *   @top: The top-level.
 *   &returns: THe new code unit.
 */
def[export] pt:st:pl3:unit(pt:st:pl3:top top) pl3:top_unit = {
	return top->unit = mem:make(st:pl3:unit{ null, top->unit });
};


/**
 * Save a top-level.
 *   @top: The top-level.
 *   @file: The output file.
 */
def[export] e:str[void](pt:st:pl3:top top, st:io file) pl3:top_save = {
	var pt:st:pl3:unit unit;

	for(unit = top->unit; unit != null; unit = unit->next)
		e:get(pl3:unit_save(unit, file));
};

/**
 * Load AST data from the top-level.
 *   @top: The top-level.
 *   @path: The file path.
 */
def[export] e:str[void](pt:st:pl3:top top, cpt:char path) pl3:top_load = {
	var ar[16]:u8 magic;
	var st:pl3:rd rd;

	rd.file = e:get(io:open(path, io:mode:read));
	onerr io:close(rd.file);

	e:get(io:read(rd.file, &magic, sizeof(magic)));

	if(!mem:equal(&magic, &pl3:magic, 16))
		e:fmt("%s: not a valid pl3 file.", path);

	while(e:get(io:rd_u8(rd.file)) != 0) {
		var pt:st:pl3:unit unit;
		var pt:st:pl3:stmt stmt;
		var pt:pt:st:pl3:stmt istmt;

		unit = pl3:top_unit(top);
		istmt = &unit->stmt;

		while((stmt = e:get(pl3:stmt_read(&rd))) != null) {
			*istmt = stmt;
			istmt = &(*istmt)->next;
		}
	}

	io:close(rd.file);
};


/**
 * Save a code unit.
 *   @top: The code unit.
 *   @file: The output file.
 */
def[export] e:str[void](pt:st:pl3:unit unit, st:io file) pl3:unit_save = {
};

